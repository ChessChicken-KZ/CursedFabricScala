plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'

	id 'babric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'scala'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "-scala" + project.scala
group = project.maven_group

loom {
	gluedMinecraftJar()
	customMinecraftManifest.set("https://babric.github.io/manifest-polyfill/${minecraft_version}.json")
}

repositories {
	maven {
		name = 'Babric'
		url = 'https://maven.glass-launcher.net/babric'
	}
	maven {
		name = 'Glass Releases'
		url = 'https://maven.glass-launcher.net/releases'
	}
	maven {
		name = "Jitpack"
		url 'https://jitpack.io/'
	}
	mavenCentral()
}

sourceSets {
	main {
		scala {
			outputDir = file("$buildDir/classes/scala/main")
			srcDirs = ['src/main/scala', 'src/main/java']
		}
		java {
			outputDir = file("$buildDir/classes/java/main")
			srcDirs = []
		}
	}
}

configurations {
	shadow_
	implementation.extendsFrom shadow_
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.glasslauncher:bin:${project.yarn_mappings}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
	implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2'

	shadow_ "org.scala-lang:scala-library:${project.scala}"
	shadow_ "org.scala-lang:scala-reflect:${project.scala}"
	shadow_ "org.scala-lang:scala-compiler:${project.scala}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

tasks.build.dependsOn tasks.shadowJar

shadowJar {
	archiveClassifier.set('')
	configurations = [project.configurations.shadow_]
}

jar {
	from("LICENSE_CFS") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}